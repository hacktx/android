buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        // The Fabric Gradle plugin uses an open ended version to
        // react quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    flatDir {
        dirs 'libs'
    }
}

def retrieveProp(String key) {
    try {
        Properties props = new Properties()
        props.load(new FileInputStream("private.props"))
        def val = props.getProperty(key)
        return val
    } catch (FileNotFoundException ignored) {
        return ''
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "hacktx.hacktx2015"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"

        buildConfigField "boolean", "IN_APP_FEEDBACK", "false"
        buildConfigField "String", "TWITTER_KEY", "\"${retrieveProp('TWITTER_KEY')}\""
        buildConfigField "String", "TWITTER_SECRET", "\"${retrieveProp('TWITTER_SECRET')}\""
        buildConfigField "String", "SLACK_KEY", "\"${retrieveProp('SLACK_KEY')}\""
        buildConfigField "String", "SLACK_TOKEN", "\"${retrieveProp('SLACK_TOKEN')}\""
        buildConfigField "String", "SLACK_ANNOUNCEMENT_ID", "\"${retrieveProp('SLACK_ANNOUNCEMENT_ID')}\""
        buildConfigField "String", "PARSE_APP_ID", "\"${retrieveProp('PARSE_APP_ID')}\""
        buildConfigField "String", "PARSE_CLIENT_ID", "\"${retrieveProp('PARSE_CLIENT_ID')}\""
        buildConfigField "String", "ESTIMOTE_APP_ID", "\"${retrieveProp('ESTIMOTE_APP_ID')}\""
        buildConfigField "String", "ESTIMOTE_APP_TOKEN", "\"${retrieveProp('ESTIMOTE_APP_TOKEN')}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile('com.twitter.sdk.android:twitter:1.6.0@aar') {
        transitive = true;
    }
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.parse.bolts:bolts-android:1.+'
    compile files('libs/Parse-1.9.1/Parse-1.9.1.jar')
    compile 'com.android.support:design:23.0.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile 'com.android.support:cardview-v7:23.0.0'
    compile 'com.android.support:recyclerview-v7:23.0.0'
    compile 'com.android.support:palette-v7:23.0.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile 'com.google.zxing:core:3.0.0'
    compile(name:'estimote-sdk', ext:'aar')
}
