buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        // The Fabric Gradle plugin uses an open ended version to
        // react quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
    flatDir {
        dirs 'libs'
    }
}

Properties props = new Properties()
try {
    props.load(file('private.properties').newDataInputStream())
} catch (Exception ex) {
    throw new GradleException("Missing private.properties, check the example.properties file.");
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "com.hacktx.android"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 3
        versionName "1.0.2"

        buildConfigField "String", "TWITTER_KEY", "\"${props.getProperty('TWITTER_KEY')}\""
        buildConfigField "String", "TWITTER_SECRET", "\"${props.getProperty('TWITTER_SECRET')}\""

        manifestPlaceholders = [ googleMapsApiKey : "${props.getProperty('GOOGLE_MAPS_KEY')}"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
    productFlavors {
        production {

        }
        dev {

        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:palette-v7:25.0.0'

    compile 'com.google.firebase:firebase-core:9.6.1'
    compile 'com.google.firebase:firebase-analytics:9.6.1'
    compile 'com.google.firebase:firebase-messaging:9.6.1'
    compile 'com.google.firebase:firebase-config:9.6.1'

    compile 'com.google.android.gms:play-services-maps:9.6.1'

    compile 'com.google.zxing:core:3.0.0'

    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.4.0'
    compile 'com.squareup.okhttp:okhttp:2.4.0'

    compile('com.crashlytics.sdk.android:crashlytics:2.6.2@aar') {
        transitive = true;
    }
    compile('com.twitter.sdk.android:twitter:2.0.0@aar') {
        transitive = true;
    }
    compile 'com.android.support.constraint:constraint-layout:1.0.0-alpha9'
}
apply plugin: 'com.google.gms.google-services'